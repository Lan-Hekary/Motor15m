/* Main.ino file generated by New Project wizard
*
* Created:   Fri Oct 9 2020
* Processor: ATmega328P
* Compiler:  Arduino AVR (Proteus)
*/

#define DOUBLE_PRESS_TIME 500

const int PUSH_IN = 3;
const int PWM_OUT  = 6;

typedef enum {
    NO_ACTION,
    RUN_ACTION,
    TOGGLE_SPEED_ACTION,
    ACTION_ENDED
} Action;

unsigned long int last_press = 0;
int motor_speed              = 50;
Action action                = NO_ACTION;

void processInput (bool pressed){
    if (pressed) {
        if (action == NO_ACTION && (millis() - last_press) < DOUBLE_PRESS_TIME) {
            action = TOGGLE_SPEED_ACTION;
        } else if (action == NO_ACTION) {
            action = RUN_ACTION;
        }
        last_press = millis();
    } else {
        action = NO_ACTION;
    }
}

void processOutput(){
    if (action == NO_ACTION) {
        analogWrite(PWM_OUT, 0);
    } else if (action == RUN_ACTION) {
        analogWrite(PWM_OUT, motor_speed);
        action = ACTION_ENDED;
    } else if (action == TOGGLE_SPEED_ACTION) {
        if (motor_speed == 50) {
            motor_speed = 200;
        } else {
            motor_speed = 50;
        }
        analogWrite(PWM_OUT, motor_speed);
        action = ACTION_ENDED;
    }
}

void setup() {
    last_press = millis();
    pinMode(PUSH_IN, INPUT_PULLUP);
    pinMode(PWM_OUT, OUTPUT);
}

void loop() {
    int input = digitalRead(PUSH_IN);

    processInput(input == LOW);

    processOutput();

    delay(10);
}
